plugins {
    id 'java'
}

group 'cn.hamster3'
version '1.0.0-SNAPSHOT'

repositories {
    maven {
        url = "https://maven.airgame.net/maven-public/"
    }
}

configurations {
    implementationShade
    implementation.extendsFrom implementationShade
}

dependencies {
    // https://mvnrepository.com/artifact/com.electronwill.night-config/core
    implementationShade group: 'com.electronwill.night-config', name: 'core', version: '3.6.4'
    // https://mvnrepository.com/artifact/com.electronwill.night-config/yaml
    implementationShade group: 'com.electronwill.night-config', name: 'yaml', version: '3.6.4'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementationShade group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    implementationShade group: 'mysql', name: 'mysql-connector-java', version: '8.0.26'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    archivesBaseName = "HamsterCurrency-Transform-Essentials"
    manifest.attributes('Main-Class': 'cn.hamster3.transform.essentials.Main')
    from([
            configurations.implementationShade.collect {
                it.isDirectory() ? it : zipTree(it)
            },
            rootProject.file("LICENSE")
    ])
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    destinationDirectory = rootProject.layout.buildDirectory
}
