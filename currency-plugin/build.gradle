//file:noinspection VulnerableLibrariesLocal
plugins {
    id 'java'
    id 'maven-publish'
}

group 'cn.hamster3'
version '2.1.1'

repositories {
    maven {
        url = "https://maven.airgame.net/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '22.0.0'
    compileOnly "cn.hamster3:HamsterService-Bukkit:2.8.3-SNAPSHOT"
    compileOnly "cn.hamster3:HamsterAPI:2.4.7-SNAPSHOT"
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly "me.clip:placeholderapi:2.10.9"

    compileOnly rootProject.fileTree(dir: './libs', includes: ['*.jar'])
}

processResources {
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

jar {
    archivesBaseName = "HamsterCurrency"
    destinationDir(rootProject.buildDir)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

javadoc {
    options.quiet()
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    options.links = [
            "https://javadoc.io/doc/org.jetbrains/annotations/21.0.1",
            'https://javadoc.io/doc/com.google.code.gson/gson/2.8.0',
            'https://ci.dmulloy2.net/job/ProtocolLib/javadoc',
            'http://milkbowl.github.io/VaultAPI',
            'https://bukkit.windit.net/javadoc'
    ]
}

publishing {
    publications {
        create("mavenJava", MavenPublication.class) {
            artifactId = "HamsterCurrency"
            artifact jar
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://maven.airgame.net/maven-releases/'
            def snapshotsRepoUrl = 'https://maven.airgame.net/maven-snapshots/'

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username maven_username
                password maven_password
            }
        }
    }
}